buildscript {
  configurations.classpath.resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}
plugins {
  id 'org.fidata.project.java'
  id 'org.fidata.project.groovy'
}
dependencies {
  api project(path: ':engine'/*, configuration: 'apiElements'*/)
  api gradleApi()
  // api group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '[2.9, 3['
  api group: 'com.fasterxml.jackson.module', name: 'jackson-module-afterburner', version: '[2.9, 3['
  api group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-guava', version: '[2.9, 3['
  api group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: '[2.9, 3['
  implementation group: 'com.google.guava', name: 'guava', version: '26.0-jre'
  // api group: 'org.apache.jclouds', name: 'jclouds-all', version: '[2, 3['
  // api(group: 'org.apache.jclouds.labs', name: 'aliyun-ecs', version: '[2, 3[').status = 'integration'
  api group: 'org.apache.jclouds.provider', name: 'aws-ec2', version: '[2, 3['
  api group: 'org.apache.jclouds.api', name: 'ec2', version: '[2, 3['
  api group: 'org.apache.jclouds.labs', name: 'azurecompute', version: '[2, 3['
  api group: 'org.apache.jclouds.labs', name: 'azurecompute-arm', version: '[2, 3['
  api group: 'org.apache.jclouds.api', name: 'cloudstack', version: '[2, 3['
  api group: 'org.apache.jclouds.provider', name: 'digitalocean2', version: '[2, 3['
  api group: 'org.apache.jclouds.api', name: 'docker', version: '[2, 3['
  api group: 'org.apache.jclouds.provider', name: 'google-compute-engine', version: '[2, 3['
  api group: 'org.apache.jclouds.labs', name: 'oneandone', version: '[2, 3['
  api group: 'org.apache.jclouds.api', name: 'openstack-nova', version: '[2, 3['
  api group: 'org.apache.jclouds.provider', name: 'profitbricks', version: '[2, 3['
  api group: 'org.apache.jclouds.labs', name: 'joyent-cloudapi', version: '[2, 3['
  api group: 'org.apache.jclouds.labs', name: 'joyentcloud', version: '[2, 3['
  api group: 'org.apache.jclouds.labs', name: 'vagrant', version: '[2, 3['
}

configurations.named('implementation').configure { exclude(group: 'org.slf4j', module: 'slf4j-api') }

/*
 * WORKAROUND:
 * https://github.com/DanySK/javadoc.io-linker/issues/5
 * `org.danilopianini.javadoc.io-linker` plugin doesn't work with `java-library` plugin
 * <grv87 2018-06-23>
 */
/*afterEvaluate {
TODO
  jvm.javadocLinks['com.fasterxml.jackson.databind'] = uri("http://fasterxml.github.io/jackson-annotations/javadoc/${ configurations.getByName('compileClasspath').resolvedConfiguration.firstLevelModuleDependencies.find { it.moduleGroup == 'com.fasterxml.jackson.core' && it.moduleName == 'jackson-databind' }.moduleVersion.replaceFirst(/.0$/, '') }/")
  // jvm.javadocLinks['com.fasterxml.jackson.annotation'] = uri("http://fasterxml.github.io/jackson-annotations/javadoc/${ configurations.getByName('compileClasspath').resolvedConfiguration.firstLevelModuleDependencies.find { it.moduleGroup == 'com.fasterxml.jackson.core' && it.moduleName == 'jackson-annotations' }.moduleVersion.replaceFirst(/.0$/, '') }/") TODO
}*/

sourceSets {
  integTest {
    groovy.srcDirs = ['src/integTest/groovy']
    resources.srcDirs = ['src/integTest/resources']
    compileClasspath += sourceSets['main'].output
    runtimeClasspath += output + sourceSets['main'].output
  }
}

sourceSets.named('integTest').configure {
  plugins.getPlugin('org.fidata.base.jvm').configureIntegrationTestSourceSetClasspath it
}
plugins.getPlugin('org.fidata.base.jvm').addJUnitDependency sourceSets.named('integTest')
plugins.getPlugin('org.fidata.base.groovy').addGroovyDependency configurations.named(sourceSets['integTest'].implementationConfigurationName)

dependencies {
  testImplementation group: 'org.apache.commons', name: 'commons-lang3', version: '[3, 4['
  testImplementation group: 'commons-io', name: 'commons-io', version: '[2, 3['
  integTestImplementation group: 'pl.pragmatists', name: 'JUnitParams', version: '[1, 2['
}

import static org.apache.commons.io.FilenameUtils.getBaseName

if (!isBuildSrc) {
  tasks.register('packerValidate-integTest')

  // TODO: CI doesn't have Packer installed
  if (System.getenv().with { !containsKey('CI') && !containsKey('JENKINS_URL') }) {
    for (File singleTemplateFile in project.fileTree('src/integTest/resources/com/github/hashicorp/packer/template') { ConfigurableFileTree fileTree ->
      fileTree.include '**/*.json'
    }) {
      TaskProvider<Task> packerValidateIntegTestSingleTemplateProvider = tasks.register("packerValidate-integTest-${ getBaseName(singleTemplateFile.toString()) }", org.fidata.gradle.packer.PackerValidate) {
        templateFile = singleTemplateFile
        syntaxOnly = true
      }
      tasks.named('packerValidate-integTest').configure { Task packerValidateIntegTest ->
        packerValidateIntegTest.dependsOn packerValidateIntegTestSingleTemplateProvider
      }
    }
  }
}

tasks.register('integTest', Test) {
  group = 'Verification'
  description = 'Runs integration tests'
  shouldRunAfter 'test' // WORKAROUND: https://github.com/FIDATA/gradle-base-plugins/issues/27 <grv87 2018-08-08>
  if (!isBuildSrc) {
    dependsOn 'packerValidate-integTest'
  }
  testClassesDirs = sourceSets['integTest'].output.classesDirs
  classpath = sourceSets['integTest'].runtimeClasspath
  reports.junitXml.destination = new File(xmlReportsDir, 'integTest')
  reports.html.destination = new File(htmlReportsDir, 'integTest')
}

tasks.named('codenarcMain').configure {
  disabledRules.add 'ClassJavadoc' // TODO
}
